buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'io.jmix.gradle:jmix-gradle-plugin:3.0.999-SNAPSHOT'
    }
}

group = 'de.bytestore'
version = '2.6.0'

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.jmix'

    repositories {
        mavenCentral()
        maven {
            url 'https://nexus.jmix.io/repository/public'
        }
    }

    jmix {
        bomVersion = '2.6.0-RC1'
        projectId = 'plugin'
    }

    group = this.group
    version = this.version

    def props = new Properties()
    buildFile.withInputStream { props.load(it) }
    def subArchivesBaseName = props.getProperty('archivesBaseName')
    def archName = subArchivesBaseName.substring(1, subArchivesBaseName.length() - 1)

    dependencies {
        api('org.pf4j:pf4j:3.13.0') {
            exclude group: 'org.slf4j', module: 'slf4j-reload4j'
        }
        api('org.pf4j:pf4j-spring:0.9.0') {
            exclude group: 'org.slf4j', module: 'slf4j-reload4j'
        }
    }

    java {
        withSourcesJar()
    }

    artifacts {
        archives jar // Registriert die kompilierten Klassen richtig
        archives sourcesJar // Erg√§nzend: Registriert die Source-JAR
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/" + System.getenv("GITHUB_REPOSITORY"))
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
        publications {
            javaMaven(MavenPublication) {
                artifactId = archName
                from components.java
            }
        }
    }

    // remove after https://youtrack.jetbrains.com/issue/IDEA-227215 is fixed
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    project.tasks.withType(JavaCompile).configureEach {
        options.compilerArgs.add("-parameters")
    }

}
